pluginManagement {
    gradle.ext.kotlinVersion = '1.5.10'
    gradle.ext.agpVersion = '4.2.2'

    plugins {
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "com.android.library" version gradle.ext.agpVersion
        id "com.automattic.android.publish-to-s3" version "test-different-s3-url"
    }
    repositories {
        maven {
            url 's3://a8c-libs.s3.amazonaws.com/android'
            credentials(AwsCredentials) {
                accessKey System.env.AWS_ACCESS_KEY
                secretKey System.env.AWS_SECRET_KEY
            }
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
        google()
    }
    resolutionStrategy {
        eachPlugin {
            // TODO: Remove this as soon as fetchstyle starts supporting Plugin Marker Artifacts
            if (requested.id.id == "com.automattic.android.fetchstyle") {
                useModule("com.automattic.android:fetchstyle:1.1")
            }
        }
    }
}
include ':WordPressUtils'
