buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.automattic.android:fetchstyle:1.1'
    }
}

apply plugin: 'com.automattic.android.fetchstyle'

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    if (tasks.findByPath('checkstyle') == null) {
        tasks.create(name: 'checkstyle', type: Checkstyle) {
            source 'src'

            classpath = files()
        }

        checkstyle {
            toolVersion = '8.3'
            configFile file("${project.rootDir}/config/checkstyle.xml")
        }
    }
}

buildScan {
    // Always run Gradle scan on CI builds
    if (System.getenv('CI')) {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
        tag 'CI'
        publishAlways()
        // Otherwise CI might shut down before it's uploaded
        uploadInBackground = false
    }
}
