plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "com.automattic.android.publish-to-s3"
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://a8c-libs.s3.amazonaws.com/android' }
}

dependencies {
    implementation "org.apache.commons:commons-text:$commonsTextVersion"
    implementation "com.android.volley:volley:$volleyVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidxSwipeRefreshLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerViewVersion"
    implementation "org.greenrobot:eventbus:$eventBusVersion"

    implementation "androidx.core:core-ktx:$androidxCoreVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core:$androidxTestCoreVersion"

    lintChecks "org.wordpress:lint:$wordpressLintVersion"

    androidTestImplementation "androidx.test:runner:$androidxTestCoreVersion"
    androidTestImplementation "androidx.test:rules:$androidxTestCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$jUnitExtVersion"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$uiAutomatorVersion"
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

project.afterEvaluate {
    publishing {
        publications {
            UtilsPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress"
                artifactId "utils"
                artifact tasks.named("androidSourcesJar")
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

